buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
        maven { url "https://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.12.0"
    }
}

apply plugin: 'docker-compose'

subprojects {
    apply plugin: 'java'
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
        maven { url "https://repo.spring.io/snapshot" }
    }

    dependencies {
        implementation(platform("io.eventuate.platform:eventuate-platform-dependencies:$eventuatePlatformVersion"))

        implementation(platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))
        implementation(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))

        compile 'commons-lang:commons-lang:2.6'

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.23.0'

    }
}

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_CDC_VERSION", eventuateCdcImageVersion
    environment.put "EVENTUATE_SAGA_VERSION", eventuateTramSagasImageVersion
    environment.put "EVENTUATE_JAVA_BASE_IMAGE_VERSION", eventuateExamplesBaseImageVersion
    environment.put "EVENTUATE_MESSAGING_KAFKA_IMAGE_VERSION", eventuateMessagingKafkaImageVersion

    projectName = System.getenv("EVENTUATE_PROJECT_NAME")
    dockerComposeStopTimeout = java.time.Duration.ofSeconds(1)

    infrastructure {
        projectName = System.getenv("EVENTUATE_PROJECT_NAME")
        startedServices = ["cdc-service", "zipkin", "mongodb"]
    }
}

subprojects.each {
    if (it.name.endsWith("-service")) {
        composeUp.dependsOn(":" + it.name + ":assemble")
    }
}

task buildAndStartServicesMySql(type: GradleBuild) {
    tasks = ["composeUp"]
}

task endToEndTests(type: GradleBuild) {
    tasks = [":end-to-end-tests:test"]
}

endToEndTests.dependsOn(composeUp)
endToEndTests.dependsOn(":end-to-end-tests:cleanTest")

task stopServicesMySql(type: GradleBuild) {
    tasks = ["composeDown"]
}
